plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    id 'org.sonarqube' version '7.0.1.6134'
}

group = 'dev.retrotv'
version = '0.0.37-alpha'

// Github Action 버전 출력용
tasks.register('printVersionName') {
    description = '이 프로젝트의 버전을 출력합니다.'
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    doLast {
        println project.version
    }
}

allprojects {
    version = rootProject.version
    group = rootProject.group

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.vanniktech.maven.publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    // 컴파일 설정 (모든 JavaCompile에 적용)
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 17
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    def springBoot = project.property('spring-boot.version')
    def uuid = project.property('uuid.version')
    def lombok = project.property('lombok.version')
    def log4j = project.property('log4j.version')
    def slf4j = project.property('slf4j.version')

    dependencies {
        // Spring Boot 라이브러리
        compileOnly "org.springframework.boot:spring-boot-starter-web:${springBoot}"
        implementation "org.springframework.boot:spring-boot-starter-jackson:${springBoot}"

        // UUID 생성 라이브러리
        implementation "com.github.f4b6a3:uuid-creator:${uuid}"

        // Logging 라이브러리
        compileOnly "org.slf4j:slf4j-api:${slf4j}"
        testImplementation "org.slf4j:slf4j-api:${slf4j}"
        testImplementation "org.apache.logging.log4j:log4j-core:${log4j}"
        testImplementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4j}"

        // Lombok 라이브러리
        compileOnly "org.projectlombok:lombok:${lombok}"
        annotationProcessor "org.projectlombok:lombok:${lombok}"
    }

    if (project.name.startsWith('framework.')) {
        mavenPublishing {
            publishToMavenCentral()
            signAllPublications()
            coordinates(group.toString(), project.name, version.toString())

            pom {
                name.set('framework-runtime')
                description.set('Spring boot 기반의 프레임워크 런타임 라이브러리입니다.')
                inceptionYear.set('2025')
                url.set('https://github.com/retrotv-framework/framework-runtime')

                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }

                developers {
                    developer {
                        id.set('yjj8353')
                        name.set('JaeJun Yang')
                        email.set('yjj8353@gmail.com')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/retrotv-framework/framework-runtime.git')
                    developerConnection.set('scm:git:ssh://github.com/retrotv-framework/framework-runtime.git')
                    url.set('https://github.com/retrotv-framework/framework-runtime.git')
                }
            }

            publishing {
                repositories {
                    maven {
                        name = 'GitHubPackages'
                        url = uri('https://maven.pkg.github.com/retrotv-framework/framework-runtime')
                        credentials {
                            username = System.getenv('USERNAME')
                            password = System.getenv('PASSWORD')
                        }
                    }
                }
            }
        }

        tasks.withType(Sign).configureEach {
            onlyIf {
                def graph = gradle.taskGraph
                    !graph.allTasks.any { task ->
                        task.path.endsWith('PublicationToMavenLocal') || task.path.endsWith('PublicationToGitHubPackagesRepository')
                    }
            }
        }
    }
}

apply from: "${rootDir}/gradle/sonarcloud.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
